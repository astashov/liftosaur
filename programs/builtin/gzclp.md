---
id: gzclp
name: GZCLP
author: Cody Lefever
url: "https://www.liftosaur.com/programs/gzclp"
shortDescription: Another good “next step” program after Basic Beginner or Starting Strength..  Do it after 3-9 months in gym.
isMultiweek: true
tags: []
frequency: 3
age: "3_to_12_months"
duration: "45-60"
goal: "strength"
---

Popular linear progression workout routine, based on the [GZCL method](http://swoleateveryheight.blogspot.com/2016/02/gzcl-applications-adaptations.html), featuring well-balanced exercise selection, and proven progression scheme. It's great for beginners, you could choose it right after 3-6 month course of [Basic Beginner](/programs/basicBeginner) program.

<!-- more -->

GZCLP is a weightlifting program based on the **GZCL principle**, created by [Cody Lefever](https://www.gainzfever.com/). The GZCL name comes from his Reddit username - [u/gzcl](https://www.reddit.com/u/gzcl). It's a great beginner program. You do it three times a week and it's based on the GZCL principle. It's a good next step after doing 3-6 months of a [Basic Beginner Routine](/programs/basicBeginner) or Starting Strength. It's also good for getting a lot of practice with exercises, because you do more reps than most other beginner programs.

## GZCL principle

Before diving in, here's some basic terminology:

- **Rep Max**: This refers to the maximum weight you can lift for a given number of reps. For instance, if you can do 5 reps (and no more) with 100 kg, then your 5 rep max is 100 kg. When measuring this, you should not go to full failure, but stop when you can't maintain good form for another rep.

Now, let's talk about exercises. Exercises in GZCL programs are split into **3 tiers**:

- **T1**: These are main compound exercises (e.g., [{Squat}], [{Deadlift}], [{Bench Press}], [{Overhead Press}]). These exercises involve the highest intensity (i.e., the largest weights, about **85-100%** of your 2-3 rep max), but with lower volume (fewer reps and sets). Typically, you will perform 10-15 total reps, usually within **1-3 reps** per set.
- **T2**: These are secondary compound exercises (e.g., [{Front Squat}], [{Romanian Deadlift}], [{Incline Bench Press}], etc). These exercises have lower intensity (lower weights), but higher volume (more reps and sets). You should pick exercises that will assist with your T1 exercises. These exercises are performed with **65-85%** of your 2-3 rep max, usually within **5-8 reps** per set.
- **T3**: These are isolation exercises (e.g., [{Leg Press}], [{Seated Leg Curl}], [{Triceps Extension}], [{Lateral Raise}]). These exercises have the lowest intensity (lightest weights), but highest volume (most reps and sets). These are performed with less than **65%** of your 2-3 rep max, usually with **8 or more reps** per set.

A useful rule of thumb is the **1:2:3 rule** - for every rep you perform in T1, do 2 reps in T2, and 3 in T3.

This is a very short description of the GZCL principle. For more information, and more details, I REALLY recommend to read [Cody's blogpost](http://swoleateveryheight.blogspot.com/2012/11/the-gzcl-method-for-powerlifting.html).

## Application of the GZCL Principle to GZCLP program

The GZCLP program has three workouts per week, but it's comprised of four different days - A1, A2, B1, and B2. So, you might do A1 on Monday, A2 on Wednesday, B1 on Friday, and then start the next week with B2 on Monday. You keep rotating through these workouts.

For **T1 exercises**, we utilize main lifts: [{Squat}], [{Deadlift}], [{Bench Press}], [{Overhead Press}]. You start with 5 sets of 3 reps. The last rep should be as many as you can do (AMRAP), but **don't push to total exhaustion**. Stop when you feel you have **1-2 reps** left. You'll know you're close to your limit when the bar gets much slower, or your form starts to go wrong.

If you manage to finish all sets, you add **5lb** to the Bench Press and Overhead Press, and **10lb** to the Squat and Deadlift. If you can't finish, you keep the weight the same, but move on to "Stage 2" - 6 sets of 2 reps. You keep adding weight following the same rules. If you still can't finish - you move to "Stage 3" - 10 sets of 1 rep.

If you can't manage "Stage 3", you either establish your new 5 reps max (5RM) and use that to restart "Stage 1", or you reduce your current weight by 10% and revert to "Stage 1".

So, to summarize:

- **Stage 1** - 5 sets of 3 reps, AMRAP on the last set
- **Stage 2** - 6 sets of 2 reps, AMRAP on the last set
- **Stage 3** - 10 sets of 1 rep, AMRAP on the last set

For **T2 exercises**, we also do the same main lifts as in T1: [{Squat}], [{Deadlift}], [{Bench Press}], [{Overhead Press}]. When you're doing Squats as T1 exercise, you do Bench Press as T2. When Bench Press is T1, Squat is T2. You flip flop the T1 and T2.

There are also 3 stages in T2, but you don't do AMRAP on the last set. You use the same progression rules as in T1 - if you successfully finish all sets, you increase the weight by **5lb** for Bench Press and Overhead Press, and by **10lb** for Squat and Deadlift. If you couldn't finish all sets, you switch to the next stage.

There're the following stages:

- **Stage 1** - 3 sets of 10 reps, no AMRAP
- **Stage 2** - 3 sets of 8 reps, no AMRAP
- **Stage 3** - 3 sets of 6 reps, no AMRAP

If you fail "Stage 3", you revert to your last weight from "Stage 1", add 15lb to it, and transition back to "Stage 1".

For the **T3 exercises**, we use [{Lat Pulldown}] and [{Bent Over Row}].

There are no stages, just do 3 sets of 15 reps, with AMRAP as a last set.

Once you can do 25 reps or more - add 5lb to the weight.

Again, this is a brief overview. For comprehensive information and details, please read the [original post with the GZCL applications](https://swoleateveryheight.blogspot.com/2016/02/gzcl-applications-adaptations.html).

<!-- faq -->

### Is GZCLP good for beginners?

Yes, GZCLP is one of the best beginner programs. It's ideal after 3-6 months of a simpler routine like Starting Strength or Basic Beginner. It introduces the tier system (T1/T2/T3) which teaches you how to structure training, and the higher rep counts give you more practice with each lift.

### How many days a week is GZCLP?

GZCLP is a 3-day program. You rotate through 4 different workouts (A1, A2, B1, B2), training 3 days per week with rest days in between. A typical schedule is Monday/Wednesday/Friday.

### How long should I run GZCLP?

Most people run GZCLP for 3-6 months. You should switch when you can no longer make linear progress (adding weight each session). At that point, move to an intermediate program like GZCL The Rippler or GZCL Jacked & Tan 2.0.

### What do I do when I stall on GZCLP?

GZCLP has a built-in stall protocol. When you fail a stage, you move to the next: Stage 1 (5x3) → Stage 2 (6x2) → Stage 3 (10x1). If you fail Stage 3, you reset to 90% of your current weight and restart at Stage 1, or test a new 5RM.

### Can I add accessories to GZCLP?

Yes. The base program has one T3 exercise per day, but you can add 1-2 more T3 exercises. Good additions include face pulls, curls, lateral raises, or ab work. Keep T3 exercises at 3x15+ with light weight.

### What is the difference between GZCLP and GZCL?

GZCL is the overarching training method created by Cody Lefever. GZCLP is a specific beginner program that applies the GZCL principles with linear progression (the "LP" stands for Linear Progression). Other GZCL programs like The Rippler and Jacked & Tan use periodization instead.

```liftoscript
# Week 1
## Day 1
// ...t1
t1: Squat / ...t1

// ...t2
t2: Bench Press / ...t2

// ...t3
t3: Lat Pulldown / ...t3

## Day 2
// ...t1
t1: Overhead Press / ...t1 / progress: custom(increase: 5lb) { ...t1 }

// ...t2
t2: Deadlift / ...t2 / progress: custom(increase: 10lb, stage3increase: 15lb) { ...t2 }

// ...t3
t3: Bent Over Row / ...t3

## Day 3
// ...t1
t1: Bench Press / ...t1 / progress: custom(increase: 5lb) { ...t1 }

// ...t2
t2: Squat / ...t2 / progress: custom(increase: 10lb, stage3increase: 15lb) { ...t2 }

// ...t3
t3: Lat Pulldown / ...t3

## Day 4
// ...t1
t1: Deadlift / ...t1

// ...t2
t2: Overhead Press / ...t2

// ...t3
t3: Bent Over Row / ...t3


// **T1**. It starts with **85% of 5RM** (or approximately **75% or 1RM**).
// You can adjust your 1RM by clicking the **1RM** link under the exercise name.
// There's the RM calculator there to help find it out if you don't know it

// **T1**.

// **T1**. Retest week, you may skip warmups. Find your new 5RM (5 rep max), as follows
// * Start with the bar, do 5 reps.
// * Throw on some more weight, do 5 reps.
// * Repeat, when the bar starts to get heavy, make smaller jumps.
// * When you finally get to a set that is hard, but you do it - take that number, and enter into "5RM Test" set weight
// * Mark it completed!
t1 / used: none / 4x3, 1x3+ / 5x2, 1x2+ / 9x1, 1x1+ / 1x5 (5RM Test) / 75% / progress: custom(increase: 10lb) {~
  if (descriptionIndex == 1) {
    descriptionIndex = 2
  }
  if (setVariationIndex == 4) {
    descriptionIndex = 2
    setVariationIndex = 1
    weights = completedWeights[1] * 0.85
    rm1 = completedWeights[1] / rpeMultiplier(5, 10)
  } else if (completedReps >= reps) {
    weights = completedWeights[ns] + state.increase
  } else if (setVariationIndex == 3) {
    descriptionIndex = 3
    setVariationIndex += 1
  } else {
    setVariationIndex += 1
  }
~}

// **T2**. Start with **35% of 1RM**.
// You can adjust your 1RM by clicking the **edit** icon, and setting the **1 Rep Max** value.
// There's the RM calculator there to help find it out if you don't know it

// **T2**.
t2 / used: none / 3x10 / 3x8 / 3x6 / 62% / progress: custom(stage1weight: 0lb, increase: 5lb, stage3increase: 10lb) {~
  if (descriptionIndex == 1) {
    descriptionIndex = 2
  }
  if (completedReps >= reps) {
    weights = completedWeights[ns] + state.increase
  } else if (setVariationIndex == 1) {
    state.stage1weight = completedWeights[ns]
    setVariationIndex += 1
  } else if (setVariationIndex == 2) {
    setVariationIndex += 1
  } else {
    setVariationIndex = 1
    weights = state.stage1weight + state.stage3increase
  }
~}

// **T3**.
t3 / used: none / 2x15, 1x15+ / 60% 90s / progress: custom(increase: 5lb) {~
  if (completedReps[ns] >= 25) {
    weights = completedWeights[ns] + state.increase
  }
~}
```
