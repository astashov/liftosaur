@top Program { expression* }

expression { LineComment | TripleLineComment | Week | Day | ExerciseExpression | EmptyExpression }

ExerciseExpression { ExerciseName Repeat? (SectionSeparator ExerciseSection?)* linebreakOrEof }
EmptyExpression { linebreak }

ExerciseName { NonSeparator+ }
ExerciseSets { CurrentVariation? ExerciseSet ("," ExerciseSet)* }
WarmupExerciseSets { (WarmupExerciseSet ("," WarmupExerciseSet)*) }
ExerciseSection {  (ExerciseProperty | ExerciseSets | ReuseSectionWithWeekDay) ("\\" linebreak)? }
ReuseSectionWithWeekDay { ReuseSection WeekDay? }
ReuseSection { "..." ExerciseName }
ExerciseSet { (Rpe | Timer | SetPart | WeightWithPlus | PercentageWithPlus | SetLabel)+ }
WeightWithPlus { Weight Plus? }
PercentageWithPlus { Percentage Plus? }
WarmupExerciseSet { (WarmupSetPart | Weight | Percentage)+ }
ExerciseProperty { ExercisePropertyName ":" (FunctionExpression | WarmupExerciseSets | None ) }
ExercisePropertyName { Keyword }
None { @specialize<Keyword, "none"> }
CurrentVariation { "!" }
WeekDay { "[" WeekOrDay (":" WeekOrDay)? "]" }
WeekOrDay { (Int | Current) }
Repeat { "[" (Rep | RepRange) ("," (Rep | RepRange))* "]" }

FunctionExpression { 
  FunctionName
  ("(" FunctionArgument? ("," FunctionArgument)* ")")?
  (Liftoscript | ReuseLiftoscript)?
}
ReuseLiftoscript { "{" ReuseSection "}" }
FunctionName { Keyword }
FunctionArgument { Number | Weight | Percentage | Rpe | RepRange | KeyValue }

PosNumber { Float | Int }
Number { (Plus | "-")? PosNumber }
RepRange { Rep "-" Rep }
Rep { Int }

Rpe { "@" (PosNumber | Plus | PosNumber Plus?) }
Percentage { PosNumber "%" }
Weight { PosNumber ("kg" | "lb") }
Timer { Int "s" }
SetPart { Rep Plus? "x" (RepRange | Rep) Plus? }
KeyValue { Keyword Plus? ":" (Number | Weight | Percentage) Plus? }
SetLabel { "(" NonSeparator+ ")" }

WarmupSetPart { (Rep "x")? Rep }

@skip { space }

@tokens {
  @precedence { Float, Int }
  @precedence { Day, Week }
  @precedence { TripleLineComment, LineComment }
  @precedence { "\\", "[", "]", NonSeparator }
  TripleLineComment { "///" ![\n]* linebreakOrEof }
  LineComment { "//" ![\n]* linebreakOrEof }
  Liftoscript { "{~" ![~]* "~}" }
  Plus { "+" }
  Week { "#" ![\n]* linebreakOrEof }
  Day { "##" ![\n]* linebreakOrEof }
  Current { "_" }
  space { $[ \t]+ }
  SectionSeparator { "/" }
  linebreakOrEof { linebreak | @eof }
  linebreak { "\n" | "\r" | "\r\n" }
  NonSeparator { ![/{}() \t\n\r#\[\]]+ }
  Keyword { $[a-zA-Z_] $[0-9a-zA-Z_]* }
  Int { @digit+ }
  Float { @digit* "." @digit+ }
}